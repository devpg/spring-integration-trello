description = 'Spring Integration Trello Support'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'org.springframework.integration'

repositories {
	maven { url 'http://repo.springsource.org/libs-milestone' }
	maven { url 'http://repo.springsource.org/plugins-release' }
	maven { url 'https://raw.github.com/joelso/joelso-mvn-repo/master/snapshots/' }
}

sourceCompatibility=1.6
targetCompatibility=1.6

ext {
	springIntegrationVersion = '2.2.3.RELEASE'
	mailVersion = '1.4'
	trelloVersion = '1.0-SNAPSHOT'
	slf4jVersion = '1.6.1'

	idPrefix = 'trello'
}


dependencies {
	compile "org.springframework.integration:spring-integration-mail:$springIntegrationVersion"
	compile "javax.mail:mail:$mailVersion"
	compile "org.trello4j:trello4j:$trelloVersion"

	// Logging
	compile "org.slf4j:slf4j-api:$slf4jVersion"
	compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	compile "org.slf4j:slf4j-simple:$slf4jVersion"
}

eclipse {
	project {
		natures += 'org.springframework.ide.eclipse.core.springnature'
	}
}

sourceSets {
	test {
		resources {
			srcDirs = ['src/test/resources', 'src/test/java']
		}
	}
}

// enable all compiler warnings; individual projects may customize further
ext.xLintArg = '-Xlint:all'
[compileJava, compileTestJava]*.options*.compilerArgs = [xLintArg]

test {
	// suppress all console output during testing unless running `gradle -i`
	logging.captureStandardOutput(LogLevel.INFO)}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}